{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Copyright (c) 2025–2026 Quan-feng WU <wuquanfeng@ihep.ac.cn>\n",
    "\n",
    "This notebook is released under the [MIT License](https://opensource.org/licenses/MIT)."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Preliminaries"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using ArbNumerics\n",
    "using CairoMakie\n",
    "using DataInterpolations\n",
    "using DelimitedFiles\n",
    "using DifferentialEquations\n",
    "using Format\n",
    "using JLD2\n",
    "using LaTeXStrings\n",
    "using SpecialFunctions\n",
    "using LambertW\n",
    "\n",
    "using NaturalUnits"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "natural_format = \"%.2e\"\n",
    "float_format = \"%.2f\"\n",
    "\n",
    "set_theme!(theme_latexfonts())"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "arbfloat_1000(x) = ArbFloat(x; bits=1000)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "include(\"tool_script-ArbNumerics_compat.jl\")\n",
    "include(\"tool_script-directories.jl\")\n",
    "include(\"tool_script-geomspace.jl\")\n",
    "\n",
    "include(\"tool_script-N_energy_density.jl\")\n",
    "include(\"tool_script-branching_ratios.jl\")\n",
    "include(\"tool_script-sterile_neutrino_production.jl\")\n",
    "\n",
    "include(\"tool_script-integrals.jl\")\n",
    "include(\"tool_script-parameters.jl\")\n",
    "\n",
    "@info \"Loaded all tool scripts.\""
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Evolution Systems"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function evolution_system_SBBN!(du, u, p, η)\n",
    "    a = exp(η)\n",
    "    log_Xₙ, log_ρ_SM_in_EU = u\n",
    "\n",
    "    Xₙ = exp(log_Xₙ)\n",
    "    ρ_SM = EU(exp(log_ρ_SM_in_EU), 4)\n",
    "\n",
    "    T_SM = from_ρ_SM_to_T_SM(ρ_SM)\n",
    "\n",
    "    x = Q / T_SM\n",
    "    Γₙ_SM = (255 / τ_n) * (12 + 6 * x + x^2) / x^5\n",
    "\n",
    "    ### Xₙ evolution ##################################\n",
    "    d_Xₙ_d_t_SM = -Γₙ_SM * (Xₙ - (1 - Xₙ) * exp(-x))\n",
    "    d_Xₙ_d_t_decay = -Xₙ / τ_n\n",
    "\n",
    "    d_Xₙ_d_t = d_Xₙ_d_t_SM + d_Xₙ_d_t_decay\n",
    "    ##################################################\n",
    "\n",
    "    ### energy density background and Hubble parameter\n",
    "    H = sqrt(ρ_SM / (3 * M_Pl^2))\n",
    "\n",
    "    d_log_ρ_SM_d_η = -4\n",
    "    ##################################################\n",
    "\n",
    "    du[1] = d_Xₙ_d_t / (H * Xₙ)\n",
    "    du[2] = d_log_ρ_SM_d_η\n",
    "    return du\n",
    "end\n",
    "\n",
    "evolution_problem_SBBN = ODEProblem(evolution_system_SBBN!,\n",
    "    map(log ∘ (v -> EUval(EU, v)),\n",
    "        [inv(1 + exp(Q / GeV(10))), (π^2 / 30) * N_ρ(GeV(10)) * GeV(10)^4]\n",
    "    ),\n",
    "    (0, log(1e7))\n",
    ")\n",
    "evolution_solution_SBBN = solve(evolution_problem_SBBN, AutoTsit5(Rosenbrock23()))\n",
    "\n",
    "a_list_SBBN = geomspace(1, 1e7, 1000)\n",
    "results_list_SBBN = (evolution_solution_SBBN ∘ log).(a_list_SBBN)\n",
    "ρ_SM_list_SBBN = map((x -> EU(x, 4)) ∘ exp ∘ last, results_list_SBBN)\n",
    "T_SM_list_SBBN = from_ρ_SM_to_T_SM.(ρ_SM_list_SBBN)\n",
    "Xₙ_list_SBBN = (exp ∘ first).(results_list_SBBN)\n",
    "\n",
    "@info \"SBBN scenario done.\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function evolution_system!(du, u, p, η)\n",
    "    a = exp(η)\n",
    "    log_t_in_EU, log_Xₙ,\n",
    "        log_ρ_SM_in_EU, log_ρ₄_in_EU, log_ρₓ_in_EU,\n",
    "        log_n_pion_minus_in_EU, log_n_pion_plus_in_EU,\n",
    "        log_n_kaon_minus_in_EU, log_n_kaon_zero_long_in_EU = u\n",
    "    m₄, U_a4_norm, Γ₄, Γₓ = p\n",
    "\n",
    "    t = EU(exp(log_t_in_EU), -1)\n",
    "    Xₙ = exp(log_Xₙ)\n",
    "    ρ_SM = EU(exp(log_ρ_SM_in_EU), 4)\n",
    "    ρ₄ = EU(exp(log_ρ₄_in_EU < -300 ? -300 : log_ρ₄_in_EU), 4)\n",
    "    ρₓ = EU(exp(log_ρₓ_in_EU), 4)\n",
    "    n_pion_minus = EU(exp(log_n_pion_minus_in_EU < -300 ? -300 : log_n_pion_minus_in_EU), 3)\n",
    "    n_pion_plus = EU(exp(log_n_pion_plus_in_EU < -300 ? -300 : log_n_pion_plus_in_EU), 3)\n",
    "    n_kaon_minus = EU(exp(log_n_kaon_minus_in_EU < -300 ? -300 : log_n_kaon_minus_in_EU), 3)\n",
    "    n_kaon_zero_long = EU(exp(log_n_kaon_zero_long_in_EU < -300 ? -300 : log_n_kaon_zero_long_in_EU), 3)\n",
    "\n",
    "    ρ_tot = ρ_SM + ρ₄ + ρₓ\n",
    "    T_SM = from_ρ_SM_to_T_SM(ρ_SM)\n",
    "\n",
    "    this_BKG_coeff = BKG_coeff(T_SM, m₄, U_a4_norm, c_ν_e)\n",
    "\n",
    "    ρ₄_eq = g₄ * numeric_energy_density_FD(T_SM / m₄) * m₄^4\n",
    "    T₄ = numeric_Tₘ_FD(ρ₄ / (g₄ * m₄^4)) * m₄\n",
    "    P₄ = iszero(T₄) ? zero(ρ₄) : g₄ * numeric_pressure_FD(T₄ / m₄) * m₄^4\n",
    "    n₄ = iszero(T₄) ? ρ₄ / m₄ : numeric_number_density_FD(T₄ / m₄) * m₄^3\n",
    "\n",
    "    Tₓ = (sqrt ∘ sqrt)(ρₓ / (gₓ * π^2 / 30))\n",
    "\n",
    "    w₄ = P₄ / ρ₄\n",
    "    # @show w₄, T₄ / m₄\n",
    "\n",
    "    γ₄ = ρ₄ / (m₄ * n₄)\n",
    "    Γ₄ /= γ₄\n",
    "    Γₓ /= γ₄\n",
    "\n",
    "    n_γ = 2 * ζ₃ * T_SM^3 / π^2\n",
    "    n_B = η_B * n_γ\n",
    "\n",
    "    x = Q / T_SM\n",
    "    Γₙ_SM = (255 / τ_n) * (12 + 6 * x + x^2) / x^5\n",
    "\n",
    "    ### Xₙ evolution ##################################\n",
    "    d_Xₙ_d_t_SM = -Γₙ_SM * (Xₙ - (1 - Xₙ) * exp(-x))\n",
    "    d_Xₙ_d_t_decay = -Xₙ / τ_n\n",
    "    d_Xₙ_d_t_pion = (1 - Xₙ) * n_pion_minus * σv_pion_minus_pton(T_SM) - Xₙ * n_pion_plus * σv_pion_plus_ntop(T_SM)\n",
    "    d_Xₙ_d_t_kaon_minus = (1 - Xₙ) * n_kaon_minus * σv_kaon_minus_pton(T_SM) - Xₙ * n_kaon_minus * σv_kaon_minus_ntop(T_SM)\n",
    "    d_Xₙ_d_t_kaon_zero_long = (1 - Xₙ) * n_kaon_zero_long * σv_kaon_zero_long_pton(T_SM) - Xₙ * n_kaon_zero_long * σv_kaon_zero_long_ntop(T_SM)\n",
    "\n",
    "    d_Xₙ_d_t = d_Xₙ_d_t_SM + d_Xₙ_d_t_decay +\n",
    "                d_Xₙ_d_t_pion + d_Xₙ_d_t_kaon_minus +\n",
    "                d_Xₙ_d_t_kaon_zero_long\n",
    "    ##################################################\n",
    "\n",
    "    ### energy density background and Hubble parameter\n",
    "    H = sqrt(ρ_tot / (3 * M_Pl^2))\n",
    "\n",
    "    d_log_t_d_η = inv(H * t)\n",
    "    d_log_ρ_SM_d_η = -4 - this_BKG_coeff * (ρ₄_eq - ρ₄) / ρ_SM + (Γ₄ - Γₓ) * ρ₄ / (H * ρ_SM)\n",
    "    d_log_ρ₄_d_η = -3 * (1 + w₄) + this_BKG_coeff * (ρ₄_eq - ρ₄) / ρ₄ - Γ₄ / H + exp(-m₄ / Tₓ) * Γₓ * ρₓ / (H * ρ₄)\n",
    "    d_log_ρₓ_d_η = -4 + Γₓ * ρ₄ / (H * ρₓ) - exp(-m₄ / Tₓ) * Γₓ / H\n",
    "    ##################################################\n",
    "\n",
    "    ### number density of mesons evolution ###########\n",
    "    this_Br_pion_minus = Br_pion_minus(m₄)\n",
    "    this_Br_pion_plus = Br_pion_plus(m₄)\n",
    "    this_Br_kaon_minus = Br_kaon_minus(m₄)\n",
    "    this_Br_kaon_zero_long = Br_kaon_zero_long(m₄)\n",
    "\n",
    "    d_n_pion_minus_d_t = n₄ * (Γ₄ - Γₓ) * this_Br_pion_minus - Γ_pion_minus * n_pion_minus -\n",
    "                            σv_pion_minus_pton(T_SM) * (1 - Xₙ) * n_B * n_pion_minus\n",
    "    d_n_pion_plus_d_t = n₄ * (Γ₄ - Γₓ) * this_Br_pion_plus - Γ_pion_plus * n_pion_plus -\n",
    "                            σv_pion_plus_ntop(T_SM) * Xₙ * n_B * n_pion_plus\n",
    "    d_n_kaon_minus_d_t = n₄ * (Γ₄ - Γₓ) * this_Br_kaon_minus - Γ_kaon_minus * n_kaon_minus -\n",
    "                            σv_kaon_minus_pton(T_SM) * (1 - Xₙ) * n_B * n_kaon_minus -\n",
    "                            σv_kaon_minus_ntop(T_SM) * Xₙ * n_B * n_kaon_minus\n",
    "    d_n_kaon_zero_long_d_t = n₄ * (Γ₄ - Γₓ) * this_Br_kaon_zero_long - Γ_kaon_zero_long * n_kaon_zero_long -\n",
    "                            σv_kaon_zero_long_pton(T_SM) * (1 - Xₙ) * n_B * n_kaon_zero_long -\n",
    "                            σv_kaon_zero_long_ntop(T_SM) * Xₙ * n_B * n_kaon_zero_long\n",
    "    ##################################################\n",
    "\n",
    "    du[1] = d_log_t_d_η\n",
    "    du[2] = d_Xₙ_d_t / (H * Xₙ)\n",
    "\n",
    "    du[3] = d_log_ρ_SM_d_η\n",
    "    du[4] = d_log_ρ₄_d_η\n",
    "    du[5] = d_log_ρₓ_d_η\n",
    "\n",
    "    du[6] = iszero(n_pion_minus) ? 0 : d_n_pion_minus_d_t / (H * n_pion_minus)\n",
    "    du[7] = iszero(n_pion_plus) ? 0 : d_n_pion_plus_d_t / (H * n_pion_plus)\n",
    "    du[8] = iszero(n_kaon_minus) ? 0 : d_n_kaon_minus_d_t / (H * n_kaon_minus)\n",
    "    du[9] = iszero(n_kaon_zero_long) ? 0 : d_n_kaon_zero_long_d_t / (H * n_kaon_zero_long)\n",
    "\n",
    "    typeof(u) == Vector{Float64} && typeof(du) == Vector{Float64} && open(\"tmp.log\", \"a+\") do io\n",
    "        println(io, \"a: \", a)\n",
    "        println(io, \"T: \", T_SM)\n",
    "        println(io, \"ρ_SM: \", ρ_SM)\n",
    "        println(io, \"ρ₄: \", ρ₄)\n",
    "        println(io, \"ρₓ: \", ρₓ)\n",
    "        println(io)\n",
    "    end\n",
    "\n",
    "    return du\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function magic(m₄, U_a4_norm, Brₓ; num_points=1000)\n",
    "    a_ini = 1\n",
    "\n",
    "    this_BKG_coeff_peak = BKG_coeff_peak(m₄, U_a4_norm)\n",
    "    T_ini = 10 * T_crit(m₄, c_ν_e)\n",
    "    @assert T_ini ≥ m₄\n",
    "    # T_ini = this_BKG_coeff_peak ≥ 1 ? max(m₄, T_fo(m₄, U_a4_norm, c_ν_e)) : T_crit(m₄, c_ν_e)\n",
    "    a_end = T_ini / eV(1)\n",
    "    # a_end = 1e2\n",
    "\n",
    "    Γ_ν = G_F^2 * m₄^5 * U_a4_norm^2 / (192 * π^3)\n",
    "    Γ_SM = Γ_ν / Br_ν(m₄)\n",
    "    Γₓ = Γ_SM * Brₓ / (1 - Brₓ)\n",
    "    Γ₄ = Γ_SM + Γₓ\n",
    "    \n",
    "    t_ini = 2.25 * NU.s * (inv ∘ sqrt)(N_ρ(T_ini) + g₄ * 7 / 8) * (MeV(1) / T_ini)^2 # Eq. (3.56) of Baumann's Cosmology\n",
    "    Xₙ_ini = inv(1 + exp(Q / T_ini))\n",
    "    ρ_SM_ini = (π^2 / 30) * N_ρ(T_ini) * T_ini^4\n",
    "    ρ₄_ini = 1e-30 * ρ_SM_ini\n",
    "    # ρ₄_ini = this_BKG_coeff_peak ≥ 1 ? numeric_energy_density_FD(T_ini / m₄) * g₄ * m₄^4 : 1e-30 * ρ_SM_ini\n",
    "    # n₄_ini = numeric_number_density_FD(T_ini / m₄) * g₄ * m₄^3\n",
    "    ρₓ_ini = 1e-30 * ρ_SM_ini\n",
    "    ρ_tot_ini = ρ_SM_ini + ρ₄_ini + ρₓ_ini\n",
    "\n",
    "    n_γ_ini = 2 * ζ₃ * T_ini^3 / π^2\n",
    "    n_meson_ini = 1e-20 * n_γ_ini\n",
    "\n",
    "    evolution_problem = ODEProblem(evolution_system!,\n",
    "        map(log ∘ (v -> EUval(EU, v)),\n",
    "            [\n",
    "                t_ini, Xₙ_ini,\n",
    "                ρ_SM_ini, ρ₄_ini, ρₓ_ini,\n",
    "                n_meson_ini, n_meson_ini, n_meson_ini, n_meson_ini\n",
    "            ]\n",
    "        ),\n",
    "        (log(a_ini), log(a_end)),\n",
    "        (m₄, U_a4_norm, Γ₄, Γₓ)\n",
    "    )\n",
    "    # evolution_solution = solve(evolution_problem, AutoTsit5(Rodas4P()))\n",
    "    evolution_solution = solve(evolution_problem, Rodas5P())\n",
    "    # evolution_solution = solve(evolution_problem, Rodas4P())\n",
    "    evolution_solution.retcode == SciMLBase.ReturnCode.Success ||\n",
    "        @warn \"$(m₄), $(U_a4_norm), $(Brₓ): $(evolution_solution.retcode)\"\n",
    "\n",
    "    a_list = geomspace(a_ini, a_end, num_points)\n",
    "    results_list = (evolution_solution ∘ log).(a_list)\n",
    "\n",
    "    t_list = map((q -> EU(q, -1)) ∘ exp ∘ (v -> v[1]), results_list)\n",
    "    Xₙ_list = map(exp ∘ (v -> v[2]), results_list)\n",
    "    ρ_SM_list = map((q -> EU(q, 4)) ∘ exp ∘ (v -> v[3]), results_list)\n",
    "    ρ₄_list = map((q -> EU(q, 4)) ∘ exp ∘ (v -> v[4]), results_list)\n",
    "    ρₓ_list = map((q -> EU(q, 4)) ∘ exp ∘ (v -> v[5]), results_list)\n",
    "    # ρ_tot_list = map((q -> EU(q, 4)) ∘ exp ∘ (v -> v[3]), results_list)\n",
    "    # ρ_SM_list = map((q -> EU(q, 4)) ∘ exp ∘ (v -> v[4]), results_list)\n",
    "    # ρ₄_list = map((q -> EU(q, 4)) ∘ exp ∘ (v -> v[5]), results_list)\n",
    "    # ρₓ_list = ρ_tot_list .- ρ_SM_list .- ρ₄_list\n",
    "\n",
    "    ρ_tot_list = ρ_SM_list .+ ρ₄_list .+ ρₓ_list\n",
    "    T_SM_list = map(from_ρ_SM_to_T_SM, ρ_SM_list)\n",
    "\n",
    "    ρ_γ_list = π^2 / 30 * 2 * T_SM_list.^4\n",
    "    ρ_ν_list = ρ_γ_list * 7 / 8\n",
    "    T_ν_decouple_first_index = findfirst(≤(MeV(0.8)), T_SM_list)\n",
    "    isnothing(T_ν_decouple_first_index) || for ii ∈ T_ν_decouple_first_index+1:length(T_SM_list)\n",
    "        ρ_ν_list[ii] = ρ_ν_list[T_ν_decouple_first_index] * (a_list[T_ν_decouple_first_index] / a_list[ii])^4\n",
    "    end\n",
    "\n",
    "    return a_list,\n",
    "        t_list, Xₙ_list,\n",
    "        ρ_tot_list,\n",
    "        ρ_SM_list, ρ₄_list, ρₓ_list,\n",
    "        T_SM_list, ρ_γ_list, ρ_ν_list,\n",
    "        Γ₄, Γ_SM, Γₓ\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Benchmark"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "m₄, U_e4_norm, Brₓ = GeV(arbfloat_1000(1.08)), arbfloat_1000(1e-5), arbfloat_1000(0.99)\n",
    "# m₄, U_e4_norm, Brₓ = GeV(1), 1e-4, 0.999\n",
    "# U_e4_norm = 1e-5\n",
    "# Brₓ = .99\n",
    "\n",
    "a_list,\n",
    "    t_list, Xₙ_list,\n",
    "    ρ_tot_list,\n",
    "    ρ_SM_list, ρ₄_list, ρₓ_list,\n",
    "    T_SM_list, ρ_γ_list, ρ_ν_list,\n",
    "    Γ₄, Γ_SM, Γₓ = magic(m₄, U_e4_norm, Brₓ)\n",
    "\n",
    "# ρ_tot_list = ρ_SM_list .+ ρ₄_list .+ ρₓ_list\n",
    "\n",
    "H_list = sqrt.(ρ_tot_list ./ (3 * M_Pl^2))\n",
    "@info \"Benchmarking done.\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "fig = Figure(size=(1000, 400))\n",
    "ax_1 = Axis(fig[1, 1],\n",
    "    title=L\"$X_n$ Evolution\",\n",
    "    xlabel=L\"$T$ [MeV]\", ylabel=L\"$X_n$\",\n",
    "    xscale=log10, # yscale=log10,\n",
    "    xreversed=true,\n",
    "    limits=((1e-2, 1e2), (-1e-2, .6)),\n",
    "    yticks=-1:.1:1,\n",
    ")\n",
    "lines!(ax_1, EUval.(MeV, T_SM_list_SBBN), Xₙ_list_SBBN; label=\"SBBN\")\n",
    "lines!(ax_1, EUval.(MeV, T_SM_list), Xₙ_list; label=\"With HNL\")\n",
    "lines!(ax_1, EUval.(MeV, [T_nuc, T_nuc]), [-0, 1]; linestyle=:dash)\n",
    "axislegend(ax_1; position=:lb)\n",
    "\n",
    "ax_2 = Axis(fig[1, 2],\n",
    "    title=\"Energy Density Evolution\",\n",
    "    xlabel=L\"$T$ [MeV]\", ylabel=L\"$\\rho / \\rho_\\nu$\",\n",
    "    xscale=log10, yscale=log10,\n",
    "    xticks=exp10.(-10:10), xtickformat=vs->[L\"10^{%$(Int(log10(v)))}\" for v in vs],\n",
    "    yticks=exp10.(-10:10), ytickformat=vs->[L\"10^{%$(Int(log10(v)))}\" for v in vs],\n",
    "    xreversed=true,\n",
    "    limits=((1e-2, maximum(EUval.(MeV, T_SM_list))), (1e-4, 1e2))\n",
    ")\n",
    "lines!(ax_2, EUval.(MeV, T_SM_list), ρ_tot_list ./ ρ_ν_list; label=L\"\\rho_\\mathrm{tot} / \\rho_\\nu\",)\n",
    "lines!(ax_2, EUval.(MeV, T_SM_list), ρ_SM_list ./ ρ_ν_list; label=L\"\\rho_\\mathrm{SM} / \\rho_\\nu\",)\n",
    "lines!(ax_2, EUval.(MeV, T_SM_list), (ρ₄_list .+ ρₓ_list) ./ ρ_ν_list; label=L\"\\left(\\rho_N + \\rho_X\\right) / \\rho_\\nu\", linestyle=:dash)\n",
    "lines!(ax_2, EUval.(MeV, T_SM_list), ρ₄_list ./ ρ_ν_list; label=L\"\\rho_N / \\rho_\\nu\", linestyle=:dash)\n",
    "lines!(ax_2, EUval.(MeV, T_SM_list), ρₓ_list ./ ρ_ν_list; label=L\"\\rho_X / \\rho_\\nu\", linestyle=:dash)\n",
    "lines!(ax_2, EUval.(MeV, [T_nuc, T_nuc]), [1e-5, 1e6]; linestyle=:dash)\n",
    "axislegend(ax_2; position=:rb)\n",
    "fig"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12.5",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
